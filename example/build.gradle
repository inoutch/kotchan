buildscript {
    ext.kotlin_version = "1.3.11"
    ext.konan_version = "1.1.0"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:3.2.1"
    }
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
}

project(":common") {
    apply plugin: "org.jetbrains.kotlin.platform.common"

    sourceSets.main.kotlin.srcDirs += "../../src/commonMain/kotlin"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    }
}

project(":jvm") {
    apply from: rootProject.file("jvm.gradle")

    /* LWJGL settings */
    applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
    mainClassName = "io.github.inoutch.kotchan.example.Main"
    defaultTasks "run"

    sourceSets.main.resources.srcDirs += "../common/src/main/resources"

    dependencies {
        expectedBy project(":common")
    }
}

project(":ios") {
    apply from: rootProject.file("ios.gradle")

    sourceSets.main {
        kotlin.srcDirs += "src/main/kotlin"
    }

    dependencies {
        expectedBy project(":common")
    }
}

project(":android") {
    apply from: rootProject.file("android.gradle")

    android.sourceSets.main {
        java.srcDirs += "src/main/kotlin"
        assets.srcDirs += "../common/src/main/resources"
    }

    dependencies {
        expectedBy project(":common")
    }
}
