// TODO remove once https://github.com/Kotlin/kotlin-frontend-plugin/issues/141 is fixed
plugins.whenPluginAdded { plugin ->
    if (plugin.class.name == "org.jetbrains.kotlin.gradle.frontend.FrontendPlugin") {
        def fixTask = tasks.register("webpack-config-fix") {
            it.doLast { file("webpack.config.d").mkdir() }
        }
        afterEvaluate {
            tasks.named("webpack-config").configure {
                it.dependsOn(fixTask)
            }
        }
    }
}

buildscript {
    ext.kotlinVersion = "1.3.61"
    ext.kotchanVersion = "0.0.1"
    ext.kotlinGLVersion = "0.0.1"
    ext.kotlinVulkanVersion = "0.0.1"
    ext.lwjglVersion = "3.2.1"
    ext.serializationVersion = "0.14.0"
    ext.coroutineVersion = "1.3.3"

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.44"
    }
}

apply plugin: "kotlinx-serialization"
apply plugin: "kotlin2js"
apply plugin: "org.jetbrains.kotlin.frontend"


repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    mavenLocal()
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.main = "call"
}

kotlinFrontend {
    npm {}
    webpackBundle {
        bundleName = "main"
        contentPath = file("build/resources/main")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serializationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutineVersion"

    implementation "io.github.inoutch:kotchan-js:$kotchanVersion"
    implementation "io.github.inoutch:kotlin-gl-js:$kotlinGLVersion"
    implementation "io.github.inoutch:kotlin-vulkan-js:$kotlinVulkanVersion"
}

sourceSets {
    main {
        kotlin.srcDirs += "../src/commonMain/kotlin"
        resources.srcDirs += "../src/commonMain/resources"
    }
}
