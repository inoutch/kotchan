allprojects {
    version "0.3.2"
    group "io.github.inoutch"
}

buildscript {
    ext.kotlin_version = "1.3.31"
    ext.lwjgl_version = "3.2.1"
    ext.serialization_version = "0.11.0"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.android.tools.build:gradle:3.4.1"
    }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjgl_natives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjgl_natives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjgl_natives = "natives-macos"
        break
}

def platform = OperatingSystem.current()
switch (platform) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjgl_natives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjgl_natives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjgl_natives = "natives-macos"
        break
}

kotlin {
    targets {
        fromPreset(presets.iosArm64, "ios") {
            compilations.main {
                cinterops {
                    vulkan {
                        includeDirs "${System.getenv()["VULKAN_SDK"]}/MoltenVK/include"
                    }
                    kmvk {
                        includeDirs "./native/MVKView/build/Release/include"
                    }
                }
            }
        }
        fromPreset(presets.jvm, "jvm")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"

                implementation "org.lwjgl:lwjgl:$lwjgl_version"
                implementation "org.lwjgl:lwjgl-stb:$lwjgl_version"
                implementation "org.lwjgl:lwjgl-glfw:$lwjgl_version"
                implementation "org.lwjgl:lwjgl-vulkan:$lwjgl_version"

                runtimeOnly "org.lwjgl:lwjgl:$lwjgl_version:$lwjgl_natives"
                runtimeOnly "org.lwjgl:lwjgl-stb:$lwjgl_version:$lwjgl_natives"
                runtimeOnly "org.lwjgl:lwjgl-glfw:$lwjgl_version:$lwjgl_natives"
                if (platform == OperatingSystem.MAC_OS) {
                    runtimeOnly "org.lwjgl:lwjgl-vulkan:$lwjgl_version:$lwjgl_natives"
                }
            }
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"
            }
        }
    }
}

// HACK: https://github.com/JetBrains/kotlin-native/issues/2314
task copyNativeLibs(type: Copy) {
    from "${System.getenv()["VULKAN_SDK"]}/MoltenVK"
    into "build/externals/MoltenVK"
}

cinteropVulkanIos.dependsOn copyNativeLibs

def repo = new File(rootDir, "repository")
publishing.repositories.maven {
    url "file://${repo.absolutePath}"
}
