allprojects {
    version "0.1.3"
    group "io.github.inoutch"
}

buildscript {
    ext.kotlin_version = "1.3.11"
    ext.lwjgl_version = "3.2.1"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:3.2.1"
    }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "maven-publish"

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
}

import org.gradle.internal.os.OperatingSystem
switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjgl_natives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjgl_natives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjgl_natives = "natives-macos"
        break
}

kotlin {
    targets {
        fromPreset(presets.iosArm64, "ios")
        fromPreset(presets.jvm, "jvm")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
                implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

                implementation "org.lwjgl:lwjgl:$lwjgl_version"
                implementation "org.lwjgl:lwjgl:$lwjgl_version:$lwjgl_natives"
                implementation "org.lwjgl:lwjgl-glfw:$lwjgl_version"
                implementation "org.lwjgl:lwjgl-glfw:$lwjgl_version:$lwjgl_natives"
                implementation "org.lwjgl:lwjgl-vulkan:$lwjgl_version"
                implementation "org.lwjgl:lwjgl-vulkan:$lwjgl_version:$lwjgl_natives"
            }
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
            }
        }
    }
}

def repo = new File(rootDir, "repository")
publishing.repositories.maven {
    url "file://${repo.absolutePath}"
}
