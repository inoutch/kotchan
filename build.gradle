version = "0.5.0"
group = "io.github.inoutch"

buildscript {
    ext.kotlinVersion = "1.3.60"
    ext.lwjglVersion = "3.2.2"
    ext.serializationVersion = "0.14.0"
    ext.coroutineVersion = "1.3.2-1.3.60"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = "https://dl.bintray.com/kotlin/kotlinx" }
        maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
        classpath "com.android.tools.build:gradle:3.5.2"
    }
}

// common
apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
// android
apply plugin: "com.android.library"
apply plugin: "kotlin-android-extensions"
// others
apply plugin: "maven-publish"

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url = "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.35.0"
}

kotlin {
    jvm {}
    js {}
    iosArm64 {
        compilations.main {
            cinterops {
                vulkan {
                    includeDirs "${System.getenv()["VULKAN_SDK"]}/MoltenVK/include"
                }
                mvkview {
                    includeDirs "./src/iosArm64Main/objc/MVKView"
                }
            }
        }
    }
    android {
        publishLibraryVariants("release")
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializationVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutineVersion"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializationVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"

                implementation "org.lwjgl:lwjgl:$lwjglVersion"
                implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"
                implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
                implementation "org.lwjgl:lwjgl-vulkan:$lwjglVersion"
            }
        }
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serializationVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutineVersion"
            }
        }
        iosArm64Main {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serializationVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutineVersion"
            }
        }
        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializationVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

                implementation "com.android.support.constraint:constraint-layout:1.1.3"
            }
        }

        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutineVersion"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            }
        }
        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion"
            }
        }
        iosArm64Test {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
            }
        }
        androidTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            }
        }
    }
}

// android
android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
    }

    packagingOptions {
        exclude "META-INF/common.kotlin_module"
    }
}

// publish configure
def repo = new File(rootDir, "repository")
publishing.repositories.maven { url "file://${repo.absolutePath}" }

// extra tasks
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

// ios
// HACK: https://github.com/JetBrains/kotlin-native/issues/2314
task copyNativeLibs(type: Copy) {
    from "${System.getenv()["VULKAN_SDK"]}/MoltenVK"
    into "build/externals/MoltenVK"
}

cinteropVulkanIosArm64.dependsOn copyNativeLibs
