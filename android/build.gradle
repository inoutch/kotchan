buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.android.tools.build:gradle:3.2.1"
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-platform-android"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "maven"

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 26
        versionName "1.0"
    }

    sourceSets.each {
        def root = "src/${it.name}"
        it.setRoot(root)
        it.java.srcDirs += "${root}/kotlin"
        it.assets.srcDirs = ["../common/src/main/resources"]
    }

    packagingOptions {
        exclude "META-INF/common.kotlin_module"
    }
}

repositories {
    jcenter()
    google()
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"
    expectedBy project(':common')
}

def repo = new File(rootDir, "repository")
uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.artifactId = "kotchan-android"
        }
    }
}